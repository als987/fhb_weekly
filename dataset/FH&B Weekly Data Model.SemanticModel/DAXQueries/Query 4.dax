DEFINE
VAR currentWeek = [LatestSalesOrdersFiscalWeek]
VAR currentStock =
    CALCULATE ( [sum_amt_mp_stock], 'Calendar'[fiscalWeek] = currentWeek ) // Create a table with rows starting at next week and going into the future
// (as long as the MP plan extends for)
VAR stockFutureWeeks =
    ADDCOLUMNS (
        FILTER (
            ALL ( 'Calendar'[fiscalWeek] ),
            'Calendar'[fiscalWeek] > currentWeek
                && 'Calendar'[fiscalWeek] <= MAX ( factForecast[fiscalWeek] )
        ),
        "cumulativeStock",
            // Store the week of the current row as a variable
            VAR tableWeek =
                CALCULATE ( SELECTEDVALUE ( 'Calendar'[fiscalWeek] ) ) // Calculate cumulative sales up to the current row week
            VAR cumulativeSales =
                CALCULATE (
                    [sum_TotSlsV] - [sum_TotPoSV] - [sum_TotStDV],
                    'Calendar'[fiscalWeek] <= tableWeek
                        && 'Calendar'[fiscalWeek] > currentWeek
                ) // Subtract cumulative sales from stock position now
            // (At some future week cumulative sales will exceed stock and the result
            // will begin to be negative from then onwards)
            VAR stockPosition = currentStock - cumulativeSales
            RETURN
                stockPosition,
        "cumulativeSales",
            VAR tableWeek =
                CALCULATE ( SELECTEDVALUE ( 'Calendar'[fiscalWeek] ) )
            VAR cumulativeSales =
                CALCULATE (
                    [sum_TotSlsV] - [sum_TotPoSV] - [sum_TotStDV],
                    'Calendar'[fiscalWeek] <= tableWeek
                        && 'Calendar'[fiscalWeek] > currentWeek
                )
            RETURN
                cumulativeSales,
        // Calculate sales (non-cumulative for the row week)
        "salesAmt",
            [sum_TotSlsV] - [sum_TotPoSV] - [sum_TotStDV]
    )
VAR posRows = 
    FILTER ( stockFutureWeeks, [cumulativeStock] > 0 )
VAR negRows =
    FILTER ( stockFutureWeeks, [cumulativeStock] < 0 ) // Filter table to only negative rows (after stock runs out)
VAR fullWeeksStock =
    COUNTROWS ( posRows ) // Count rows to get total number of WHOLE weeks of future sales the current stock can cover
VAR lastFullWeek =
    TOPN ( 1, SUMMARIZE ( posRows, [cumulativeStock] ), [cumulativeStock], ASC ) // Store the smallest value of cumulative stock (which will be the week just before stock runs out) 
// Two step process to get sales value for the week just after stock runs out 
VAR firstNegWeek =
    TOPN ( 1, SUMMARIZE ( negRows, [fiscalWeek], [salesAmt] ), [fiscalWeek], ASC ) // Need to order by calendar date so first get the value of the calendar date for the row
VAR firstNegWeekSales =
    TOPN ( 1, SUMMARIZE ( firstNegWeek, [salesAmt] ), [salesAmt], ASC ) // Return the actual sales value of the week
// Number of weeks of stock cover is a partial week consisting of number of WHOLE weeks + fraction of the next week of sales the remaining stock can cover
VAR result = fullWeeksStock + DIVIDE ( lastFullWeek, firstNegWeekSales )
EVALUATE
posRows
//stockFutureWeeks
ORDER BY 'Calendar'[fiscalWeek]